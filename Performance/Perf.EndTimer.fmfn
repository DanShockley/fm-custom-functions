/**
 *
 * End performance time measurement and record results to a JSON object
 *
 * @param {text} tag - unique tag that the end timer function will reference
 * @param {text} objectVarName - the performance JSON object or array
 * @param {text} [alternateElementKey] - OPTIONAL: JSON element key under which to store the result ; DEFAULT = tag
 *
 * @module Performance (Perf)
 * 
 * @version 1.1.0
 * @author Allen Ulbricht, https://github.com/aulbricht
 *
 */


Let ([

	//////////////////////////////////////////
	~tag = tag ;
	~objName = objectVarName ;
	~key = alternateElementKey ;
	//////////////////////////////////////////


	~tag_converted = Filter ( Base64Encode ( ~tag ) ; "abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ_" );
	~varName_temp = "performance." & ~tag_converted ;
	~key = If ( IsEmpty ( ~key ) ; ~tag ; ~key );

	~end_ms = Get ( CurrentTimeUTCMilliseconds );
	~start_ms = Let ( ~v = Evaluate ( "$$" & ~varName_temp ); If ( IsEmpty ( ~v ); ~end_ms ; ~v ) );
	~elapsed_ms = ~end_ms - ~start_ms ;

	~objScope = Filter ( ~objName ; "$" );
	~objectVar = If ( IsEmpty ( ~objScope ) ; "$" ) & ~objName ;
	~objectVarValue = Evaluate ( ~objectVar );

	~subStr = 
		"Let ([¶
			{{objectVar}} = If ( Left ( {{objectVar}} ; 1 ) = \"?\" ; \"{}\" ; {{objVarValue}} );¶
			{{objectVar}} = ¶
				JSONSetElement ( {{objectVar}} ;¶
					[ \"{{key}}.start_ms\" ; {{start_ms}} ; JSONNumber ];¶
					[ \"{{key}}.end_ms\" ; {{end_ms}} ; JSONNumber ];¶
					[ \"{{key}}.elapsed_ms\" ; {{elapsed_ms}} ; JSONNumber ];¶
					[ \"{{key}}.elapsed_s\" ; {{elapsed_s}} ; JSONNumber ]¶
					);¶
			~=\"\"]; \"\" )";
	~evalStr = 
		Substitute ( ~subStr ;
			[ "{{objectVar}}" ; ~objectVar ];
			[ "{{objVarValue}}" ; Quote ( ~objectVarValue ) ];
			[ "{{key}}" ; ~key ];
			[ "{{start_ms}}" ; ~start_ms ];
			[ "{{end_ms}}" ; ~end_ms ];
			[ "{{elapsed_ms}}" ; ~elapsed_ms ];
			[ "{{elapsed_s}}" ; Round ( ~elapsed_ms / 1000 ; 3 ) ]
			);

	~_void = Evaluate ( "Let ( $$" & ~varName_temp & "=\"\" ; \"\" )" );

	~evalResult = EvaluationError ( Evaluate ( ~evalStr ) );

~=""];

~elapsed_ms

)