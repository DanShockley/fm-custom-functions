/********
 * 
 * Tests the object for an errorCode
 * 
 * @param {object} errorObject - JSON error object or array
 * @param {text or number} cell - OPTIONAL: specify which array cell to test
 * 									Valid parameter values:
 * 										"any" [default]
 * 										"last" / "n" - last (most recent) array cell
 * 										1, 2..[n] - numeric cell value (1 = array cell 0, etc)
 * 
 * @return {boolean} TRUE if any errorCode is not 0 or NULL
 * 
 * @note errorObject can also be a simple integer (e.g. 1 )
 * 
 * @module Error
 * 
 * @version 2.0.1
 * @author Allen Ulbricht, https://github.com/aulbricht
 * 
 ********/


Let ([

	~eo = errorObject ;
	~cell = cell ;

	// process cell specification
	~isArray = Left ( JSONFormatElements ( ~eo )  ; 1 ) = "[" ;
	~eo = Case (
			not ~isArray ; ~eo ; // if the object is anything except an array, skip this step
			~cell = "last" ; JSONGetElement ( ~eo ; "[" & ValueCount ( JSONListKeys ( ~eo ; "." )) - 1 & "]" );
			~cell > 0 and ~cell = GetAsNumber ( ~cell ) ; JSONGetElement ( ~eo ; Int ( ~cell ) - 1 );
			~eo
			);

	// determine error object input type
	~isObject = Left ( JSONFormatElements ( ~eo )  ; 1 ) = "{" ;
	~isArray = Left ( JSONFormatElements ( ~eo )  ; 1 ) = "[" ;
	~isJSON = ~isObject or ~isArray ;


	~errorNonJSON = If ( not ( ~isJSON ) ; ~eo );
	~LIST_keys = JSONListKeys ( ~eo ; "." );
	~numKeys = Case ( 
			~isJSON ; ValueCount ( ~LIST_keys ); 
			IsEmpty ( ~eo ) ; 0 ;
			1
			);

	~currentErrorCode = If ( ~isJSON ; 
					JSONGetElement ( ~eo ;  If ( ~isArray ; "[0]." ) & "errorCode" );
					~errorNonJSON 
					);

	~errorCode = Case ( 
			~isJSON ; 
				Case ( 
					~numKeys > 1 ; Abs ( ~currentErrorCode ) + 

						/////////////////////////////////////////////////////////
						// RECURSION: ///////////////////////////////////////////

						Error.IsError ( 
							JSONDeleteElement ( 
								~eo ; 
								If ( ~isJSON ; GetValue ( ~LIST_keys ; 1 ) ; "[0]" ) 
								);
							""
							);

						/////////////////////////////////////////////////////////
						/////////////////////////////////////////////////////////

					~numKeys = 1 ; JSONGetElement ( ~eo ; If ( ~isArray ; "[0]." ) & "errorCode" );
					0
					);
				~errorNonJSON
			);
			
	~errorCodeExists = not IsEmpty ( ~errorCode ) and ~errorCode â‰  0 ;

~=""];

Case (

	// if there is no error object
	IsEmpty ( ~eo ) ; False ;

	// if the error code(s) is/are 0
	~errorCode = 0 ; False ;

	// if a non-zero error code exists
	~errorCodeExists ; True ;

	// default return false
	False

	)

)