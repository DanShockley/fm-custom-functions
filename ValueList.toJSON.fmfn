/**
 * VERSION:			1.0.0
 * Function Name: 	ValueList.toJSON()
 * Name Dependency: yes (i.e. recursion, must update the code if renaming the function)
 * Purpose: 		Convert value list to a JSON array
 *
 * @param {string}	valueList = return delimited value list
 * @param {string}	jsonKey = key string under which to store the values, e.g. "names.first" (or "." for root)
 * @param {string}	format = "object" or "array"
 *
 * @returnSuccess {integer} length of the array
 * @returnError {JSON Array} error[n].code = error code
 * @returnError {JSON Array} error[n].description = description of error
 * @returnError {JSON Array} error[n].notes = additional notes about the error
 * 
 * @author Allen Ulbricht, https://github.com/aulbricht
 * @website http://northcoastsoftware.com
 *
 */

Let([

	~list = valueList ;
	~key = jsonKey ;
	~format = format ;

	~value = RightValues ( ~list; 1 );
	~value = If ( Right ( ~value ; 1 ) = "¶" ; Left ( ~value ; Length ( ~value ) - 1 ); ~value );
	~count = ValueCount ( ~list );
	~listUpdated = LeftValues ( ~list ; ~count - 1 );
	~jsonInitString =  If ( ~format = "object" ; "{}" ; "[]" );

~=""];

Case ( 

	~count = 0 ; If ( ~key = "." or IsEmpty ( ~key ) ; ~jsonInitString ; "" ) ;
	~count > 0 ;
		JSONSetElement (  
			// RECURSION
			ValueList.toJSON ( ~listUpdated ; ~key ; ~format ) ;

			// array cell
			~key & If ( Right ( ~key ; 1 ) ≠ "." ; "." ) & If ( ~format = "object" ; ~count ; "[" & ~count - 1 & "]" ) ;

			// value to store
			~value ;

			// Determine the data type
			Case ( 
				Left ( JSONFormatElements ( ~value ) ; 1 ) ≠ "?" and Left ( ~value ; 1 ) = "{" ; JSONObject ;
				Left ( JSONFormatElements ( ~value ) ; 1 ) ≠ "?" and Left ( ~value ; 1 ) = "[" ; JSONArray ;
				GetAsText ( GetAsTimestamp ( ~value ) ) = ~value ; JSONString;
				GetAsNumber ( ~value ) = ~value ; JSONNumber;
				GetAsText ( ~value ) = ~value ; JSONString;
				)
	 	)

)

)