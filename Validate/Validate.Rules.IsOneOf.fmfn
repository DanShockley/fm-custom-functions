/**
 *
 * A rule that tests whether a parameter value exists is in a list of valid values
 *
 * @param {string} key = the JSON key path to the value to test
 * @param {string} LIST_values = value list to validate against
 *
 * @returnSuccess {NULL}
 * @returnError {object} Validate.Error: an error object
 * 
 * @module Validate
 * @requires {function} Validate._errorCreate()
 * 
 * @version 1.1.1
 * @author Allen Ulbricht, https://github.com/aulbricht
 * @credit forked from code by original author Todd Geist
 *
 */


Let ([

	~key = key ;
	~list = LIST_values ;

	~value = JSONGetElement(  $validate.this ; ~key  )

];

Case (

	// if value is NULL, return NULL 
	IsEmpty ( ~value ) ; "" ; 

	// if value exists in the list, return NULL 
	PatternCount ( "¶" & ~list & "¶"; "¶" & ~value & "¶" ) > 0 ; "" ; 

	// if value does not exist in the list, return error
	Validate._errorCreate ( ~key ; "\"" & ~key & "\" should be one of: " & Substitute( ~list ; "¶"; ", " ) & ". Instead it was: " & If ( IsEmpty( ~value ) ; "EMPTY STRING" ; ~value ) )

)

)