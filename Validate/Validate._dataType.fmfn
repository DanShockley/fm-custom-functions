/**
 *
 * Return a LIST of valid data types for the given value (e.g. 0 or 1 would be valid for both "number" and "boolean")
 *
 * @param {string} value = data to evaluate for type
 * 
 * @returns {string} LIST of valid data types
 *
 * @private
 * @module Validate
 *
 * @version 2.0.0
 * @author Allen Ulbricht, https://github.com/aulbricht
 *
 */


Let ([

	// Abstract parameter
	~value = value ;

	~baseDataType = Case ( 

				// empty value
				IsEmpty ( ~value ) ; "null" ;

				// JSON object types
				Left ( JSONFormatElements ( ~value ) ; 1 ) ≠ "?" and Left ( ~value ; 1 ) = "{" ; "object" ;
				Left ( JSONFormatElements ( ~value ) ; 1 ) ≠ "?" and Left ( ~value ; 1 ) = "[" ; "array" ;

				// timestamp
				GetAsText ( GetAsTimestamp ( ~value ) ) = ~value ; "timestamp" ;

				// date
				GetAsText ( GetAsDate ( ~value ) ) = ~value ; "date" ;

				// time
				GetAsText ( GetAsTime ( ~value ) ) = ~value ; "time" ;

				// number
				GetAsNumber ( ~value ) = ~value ; "number" ; 

				// text
				GetAsText ( ~value ) = ~value ; "text" ;

				);

	// add check for higher order data types (e.g. "boolean" which could also be true if validating as "number") an special cases
	~higherOrderDataType = Case ( 

				// boolean / number
				~baseDataType = "number" and ( ~value = 0 or ~value = 1 ); "boolean" ;

				// number / text
				~baseDataType = "number" ; "text" ;

				// date / text
				~baseDataType = "date" ; "text" ;

				// time / text
				~baseDataType = "time" ; "text" ;

				// timestamp / text
				~baseDataType = "timestamp" ; "text" ;

				); 

~=""];

List (

	~basedataType ;
	~higherOrderDataType 

	)

)