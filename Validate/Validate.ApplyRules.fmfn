/**
 *
 * Apply a list of rules to the JSON set by JSON.Validate.This()
 *
 * @param {string} LIST_rules = value list of rules, e.g. List ( Validate.Rule.<x> ; … )
 *
 * @returnSuccess {NULL}
 * @returnError {object} Validate.Error: an error object
 * 
 * @module Validate
 * @requires {function} Validate._errorCreate()
 * 
 * @version 2.0.1
 * @author Allen Ulbricht, https://github.com/aulbricht
 * @credit forked from code by original author Todd Geist
 *
 */


Let ([

	~data = $$validate.this ;
	~LIST_errors = LIST_rules ;

	~formattedData = If ( not IsEmpty ( ~data ) ; JSONFormatElements ( ~data ) );

	// clear the known validation variable
	$$validate.this = ""

];

Case (

	// check for invalid JSON
	Left ( ~formattedData ; 1 ) = "?" ; Validate._errorCreate( "" ; List ( "JSON To Validate is Invalid." ; ~formattedData ) );

	// if there are no errors
	IsEmpty( ~LIST_errors ) ; "";

  	// if there are errors, return error object
	Let ([
		~numErrors = ValueCount ( ~LIST_errors );
		~ARRAY_errors = "[" & Substitute( ~LIST_errors ; "¶" ; "," ) & "]" 
	];

	Case ( 
		~numErrors > 1 ; ~ARRAY_errors ;
		Left ( JSONFormatElements ( ~LIST_Errors ) ; 1 ) ≠ "?" ; JSONFormatElements ( ~LIST_Errors ) ;
		~LIST_Errors
		)

	)
)

)